plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'org.liquibase.gradle' version '2.0.1'
    id 'java'

    id "com.github.node-gradle.node" version "3.0.0-rc2"
}

apply plugin: 'io.spring.dependency-management'

group = 'by.rjeey'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '10'
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

node {
    download = true

    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.projectDir}/npm")
}

processResources {
    dependsOn 'copyFrontendToBuild'
}

task copyFrontendToBuild(type: Copy) {
    from "src/main/frontend/build"
    into "build/resources/main/static/."
}

task appNpmBuild(type: NpmTask) {
    workingDir =file("${project.projectDir}/src/main/frontend")
    args = ["run", "build"]
}

task appNpmInstall(type: NpmTask) {
    ignoreExitValue = true
    workingDir = file("${project.projectDir}/src/main/frontend")
    args = ['install']
}

appNpmBuild.dependsOn appNpmInstall
copyFrontendToBuild.dependsOn appNpmBuild
compileJava.dependsOn copyFrontendToBuild

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.0'
    runtimeOnly 'mysql:mysql-connector-java'
    liquibaseRuntime 'mysql:mysql-connector-java'
    implementation 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
}
